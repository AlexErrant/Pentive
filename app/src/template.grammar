@top Document { entity+ }

entity {
  Text |
  Element |
  MismatchedCloseTag |
  incompleteStartCloseTag
}

Element {
  OpenTag entity* (CloseTag | MissingCloseTag) |
  SelfClosingTag
}

OpenTag[closedBy="CloseTag MissingCloseTag",isolate] {
  StartTag space* TagName space* EndTag
}

SelfClosingTag[isolate] {
  StartTag space* TagName space* EndTag 
}

CloseTag[openedBy=OpenTag,isolate] {
  StartCloseTag space* TagName space* EndTag
}

MismatchedCloseTag[isolate] {
  mismatchedStartCloseTag space* TagName space* EndTag
}

@context elementContext from "./template.tokens.ts"

@external tokens startTag from "./template.tokens.ts" {
  StartTag[closedBy="EndTag"]
  StartCloseTag
  MissingCloseTag
  mismatchedStartCloseTag[@name=StartCloseTag]
  incompleteStartCloseTag[@name=StartCloseTag]
}

@tokens {
  EndTag[openedBy="StartTag StartCloseTag"] { ">" }
  TagName { @asciiLetter+ }
  Text { ![<&]+ }
  space { (" " | "\t" | "\r" | "\n")+ }
}

@external propSource xmlHighlighting from "./template.highlight.ts"

@detectDelim
