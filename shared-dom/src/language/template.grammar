@top Document { entity+ }

entity {
  Text |
  Brace |
  Element |
  MismatchedCloseTag |
  incompleteStartCloseTag
}

Element {
  OpenTag entity* (CloseTag | MissingCloseTag) |
  SelfClosingTag
}

OpenTag[closedBy="CloseTag MissingCloseTag",isolate] {
  StartTag (If | Unless) TagName EndTag
}

SelfClosingTag[isolate] {
  StartTag Transformer* TagName EndTag
}

CloseTag[openedBy=OpenTag,isolate] {
  StartCloseTag TagName EndTag
}

MismatchedCloseTag[isolate] {
  mismatchedStartCloseTag TagName EndTag
}

@context elementContext from "./template.tokens.js"

@external tokens startTag from "./template.tokens.js" {
  StartTag[closedBy="EndTag"]
  StartCloseTag
  MissingCloseTag
  mismatchedStartCloseTag[@name=StartCloseTag]
  incompleteStartCloseTag[@name=StartCloseTag]
}

Transformer { TagName* TransformerDelimiter }

@tokens {
  EndTag[openedBy="StartTag StartCloseTag"] { "}}" }
  TagName { ![}:#^] ![}:]* } // if you update this, also update 08643083-B4CC-42E8-ACFA-A713DF287B7F
  TransformerDelimiter { ":" }
  If { "#" }
  Unless { "^" }
  Text { ![{]+ }
  Brace[@name=Text] { "{" }
}

@external propSource xmlHighlighting from "./template.highlight.js"

@detectDelim
